#DEFINITIONS:  -*-sh-*-
#
# Tests iyActiveSingleScat with respect to Jacobian calculations
#
# The input files to this test were generated by setup_wfun_test.m
#
Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Read data from files
ReadXML( p_grid, "testdata/p_grid_wfuntest.xml" )
ReadXML( particle_bulkprop_field, "testdata/particle_bulkprop_field.xml" )
ReadXML( particle_bulkprop_names, "testdata/particle_bulkprop_names.xml" )
ReadXML( f_grid, "testdata/f_grid_wfuntest.xml" )
ReadXML( scat_data_raw, "testdata/scat_data_wfuntest.xml" )
ReadXML( scat_meta, "testdata/scat_meta_wfuntest.xml" )

# Some basic settings
AtmosphereSet1D
IndexSet( stokes_dim, 2 )

# Agenda for scalar gas absorption calculation
Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)

# on-the-fly absorption
Copy( propmat_clearsky_agenda, propmat_clearsky_agenda__OnTheFly )

# Definition of species
abs_speciesSet( species=[ "N2-SelfContStandardType",
                          "O2-PWR93",
                          "H2O-PWR98"
                        ] )

# No line data needed here
abs_lines_per_speciesSetEmpty

# Atmospheric profiles
AtmRawRead( basename = "testdata/tropical" )
#
AtmFieldsCalc

# Get ground altitude (z_surface) from z_field
Extract( z_surface, z_field, 0 )


# scat_species and pnd_agenda
#
StringCreate( species_id_string )
#
# Scat species 0
StringSet( species_id_string, "IWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "IWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq",
                           x_fit_start=100e-6 )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdMH97( t_min = 10, t_max = 273, t_min_psd = 210 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )
#
scat_dataCalc
scat_data_checkedCalc

# Set Jacobian quantities
#
VectorCreate( rgrid )
Copy( rgrid, p_grid )
#
jacobianInit
#
jacobianAddScatSpecies( 
    species  = "IWC",
    quantity = "IWC",
    g1 = rgrid,
    g2 = lat_grid,
    g3 = lon_grid
)
jacobianAddAbsSpecies( 
    species  = "H2O-PWR98",
    unit     = "vmr",
    g1       = rgrid,
    g2       = lat_grid,
    g3       = lon_grid
)
jacobianAddTemperature( 
    hse = "off",
    g1  = rgrid,
    g2  = lat_grid,
    g3  = lon_grid
)
#
jacobianClose

# pnd and cloudbox
cloudboxSetFullAtm
pnd_fieldCalcFromParticleBulkProps

# Define radar
#
sensorOff
#
MatrixSet( sensor_pos, [400e3] )
MatrixSet( sensor_los, [180] )
#
ArrayOfIndexSet( instrument_pol, [ 5 ] )
Append( instrument_pol_array, instrument_pol )
#
AgendaSet( iy_transmitter_agenda ){
  Ignore( rtp_pos )
  Ignore( rtp_los )
  iy_transmitterSinglePol
}
#
AgendaSet( iy_main_agenda ){
  Ignore( iy_id )
  Ignore( rte_pos2 )
  Ignore( nlte_field )
  Ignore( iy_unit )
  ppathPlaneParallel( cloudbox_on = 0 )
  iyActiveSingleScat( trans_in_jacobian = 1 )
}
#
StringSet( iy_unit, "dBZe" )
#
VectorLinSpace(range_bins, 0, 15e3, 500 )

# Perform other checks
abs_xsec_agenda_checkedCalc
propmat_clearsky_agenda_checkedCalc
atmfields_checkedCalc
atmgeom_checkedCalc
cloudbox_checkedCalc
sensor_checkedCalc

# Perform calcuations
yActive

}
