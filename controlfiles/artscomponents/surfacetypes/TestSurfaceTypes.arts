# This is a basic demonstration of how to work with surface types.
#
#    In short, the geographical distribution of the surface types is
#    set by *surface_type_mask* and the radiative properties of each
#    type is specified by *iy_surface_agenda_array*.
#
#    The radiative properties can also be specified by
#    *surface_rtprop_agenda_array*.


# Patrick Eriksson 2020-09-20

Arts2{

  # Includes
  INCLUDE "general/general.arts"
  INCLUDE "general/agendas.arts"
  INCLUDE "general/continua.arts"
  INCLUDE "general/planet_earth.arts"

  # Agendas (excpept surface ones)
  Copy(ppath_agenda, ppath_agenda__FollowSensorLosPath)
  Copy(ppath_step_agenda, ppath_step_agenda__GeometricPath)
  Copy(iy_space_agenda, iy_space_agenda__CosmicBackground)
  Copy(iy_main_agenda, iy_main_agenda__Emission)
  Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)
  Copy(propmat_clearsky_agenda, propmat_clearsky_agenda__OnTheFly)

  # Various basic settings
  AtmosphereSet3D
  IndexSet(stokes_dim, 2 )
  VectorSet( f_grid, [89e9] )
  sensorOff
  AntennaOff
  jacobianOff
  cloudboxOff
  StringSet(iy_unit, "PlanckBT")

  # Absorption
  abs_speciesSet(species=["N2-SelfContStandardType","O2-PWR98","H2O-MPM89"])
  abs_lines_per_speciesSetEmpty  

  # Atmosphere and surface altitude
  VectorNLogSpace( p_grid, 35, 1013e2, 100e2 )
  VectorLinSpace( lat_grid, -2, 8, 0.5 )
  VectorLinSpace( lon_grid, -35, 25, 1 )
  AtmRawRead(basename="testdata/tropical")
  AtmFieldsCalcExpand1D(vmr_zeropadding=1)
  #
  z_surfaceConstantAltitude( altitude = 0 )

  # Read in variables defining surface properties
  ReadXML(surface_type_mask, "surface_type_mask.xml")
  GriddedField2Create(surface_w_field)
  ReadXML(surface_w_field, "surface_w_field.xml")
  GriddedField2Create(surface_t_field)
  ReadXML(surface_t_field, "surface_t_field.xml")
  TessemNNReadAscii(tessem_neth,"testdata/tessem_sav_net_H.txt")
  TessemNNReadAscii(tessem_netv,"testdata/tessem_sav_net_V.txt")

  # Agendas for the surface
  AgendaSet(iy_surface_agenda){
    SurfaceDummy
    Ignore(nlte_field)
    surface_typeInterpTypeMask(method="linear")
    iySurfaceCallAgendaX
  }
  NumericCreate( wind_speed )
  ArrayOfAgendaAppend( iy_surface_agenda_array ){
    Ignore( agenda_array_index )
    Ignore( surface_type_aux )
    InterpGriddedField2ToPosition( out=surface_skin_t,
                             gfield2=surface_t_field )
    InterpGriddedField2ToPosition( out=wind_speed,
                             gfield2=surface_w_field )
    surfaceTessem( wind_speed = wind_speed )
    iySurfaceRtpropCalc
  }
  ArrayOfAgendaAppend( iy_surface_agenda_array ){
    SurfaceDummy
    Ignore( agenda_array_index )
    specular_losCalc( ignore_surface_slope=1 )
    InterpGriddedField2ToPosition( out=surface_skin_t,
                           gfield2 = surface_t_field )
    VectorSetConstant( surface_scalar_reflectivity, 1, surface_type_aux )
    surfaceFlatScalarReflectivity
    iySurfaceRtpropCalc
  }

  # Sensor pos and los
  MatrixSet(sensor_pos, [800e3,4,-32;800e3,4,-32])
  MatrixSet(sensor_los, [179,90;150,90])

  # Checks
  lbl_checkedCalc
  abs_xsec_agenda_checkedCalc
  propmat_clearsky_agenda_checkedCalc
  atmfields_checkedCalc
  atmgeom_checkedCalc
  cloudbox_checkedCalc
  sensor_checkedCalc

  yCalc
  #WriteXML("ascii" , y, "y.xml")
  Print(y,0)
}
