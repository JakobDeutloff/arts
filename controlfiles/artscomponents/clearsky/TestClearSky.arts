#DEFINITIONS:  -*-sh-*-
#
# Demonstration and test of simple ARTS 1D, 2D and 3D clear sky calculations.
#
# Observations from a satellite is treated, with three viewing directions:
#   1: Cold space (ie. above the model atmosphere)
#   2: Limb sounding
#   3: Downward observation.
#
# For the test sequence (ie. "make check") this is a first test on that the
# full chain around yCalc is working. Calculation of optical depth as an
# auxilary variable is included and tested.
#
# Author: Patrick Eriksson


Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"


# Number of Stokes components to be computed
#
IndexSet( stokes_dim, 1 )

# Clearsky = No scattering
#
cloudboxOff

# No jacobian calculation
#
jacobianOff


# Read a line file and a matching small frequency grid
# ---
ReadXML( abs_lines, "abs_lines.xml" )
VectorNLinSpace( f_grid, 5, 320e9, 322e9 )


# A pressure grid rougly matching 0 to 80 km, in steps of 2 km.
# ---
VectorNLogSpace( p_grid, 41, 1000e2, 1 )


# Definition of species
# ---
SpeciesSet( abs_species,
            ["H2O-SelfContStandardType, H2O-ForeignContStandardType, H2O", 
             "N2-SelfContStandardType",
             "O3"] )


# Sort the line file according to species
# ---
abs_lines_per_speciesCreateFromLines


# Atmospheric scenario
# ---
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, 
            "testdata/tropical" )


# Weakly reflecting surface (surface_skin_t set below in 1D part)
# ---
VectorSetConstant( surface_scalar_reflectivity, 1, 0.8 )
AgendaSet( surface_rtprop_agenda ){
  Ignore( rte_pos )
  surfaceFlatScalarReflectivity
} 


# No sensor properties
# ---
sensorOff


# We select here to use Rayleigh-Jean brightness temperatures
# ---
StringSet( iy_unit, "RJBT" )


# Extract radiative nackground and optical depth as auxilary variables
# ---
ArrayOfStringSet( iy_aux_vars, [ "Optical depth", "Radiative background" ] )
# Create vector container for the optical depth
VectorCreate( odepth )



#########################################################################
# 1D
#########################################################################

# Atmosphere and surface
# ---
AtmosphereSet1D
AtmFieldsCalc
Extract( z_surface, z_field, 0 )

# *surface_skin_t*
# ---
Extract( t_surface, t_field, 0 )
VectorCreate( t_surface1D )
VectorExtractFromMatrix( t_surface1D, t_surface, 0, "row" )
Extract( surface_skin_t, t_surface1D, 0 )

# Definition of sensor position and LOS
# ---
MatrixSetConstant( sensor_pos, 3, 1, 600e3 )
MatrixSet( sensor_los, [ 95; 113; 135] )

# Perform RT calculations
# ---
basics_checkedCalc
cloudbox_checkedCalc
yCalc

# OK?
# ---
Extract( odepth, y_aux, 0 )
#WriteXML( "ascii", y, "yREFERENCE_1D.xml" )
#WriteXML( "ascii", odepth, "y_auxREFERENCE_1D.xml" )
VectorCreate( yREFERENCE )
ReadXML( yREFERENCE, "yREFERENCE_1D.xml" )
VectorCompare( y, yREFERENCE, 1e-2 )
ReadXML( yREFERENCE, "y_auxREFERENCE_1D.xml" )
VectorCompare( odepth, yREFERENCE, 1e-3 )



#########################################################################
# 2D
#########################################################################

# Atmosphere and surface
# ---
AtmosphereSet2D
VectorLinSpace( lat_grid, -45, 45, 1 )
AtmFieldsCalcExpand1D
Extract( z_surface, z_field, 0 )
refellipsoidEarth( refellipsoid, "WGS84" )

# Definition of sensor position and LOS
# ---
MatrixCreate( zeros )
MatrixSetConstant( zeros, 3, 1, 0 )
Append( sensor_pos, zeros, "trailing" )

# Perform RT calculations
# ---
basics_checkedCalc
cloudbox_checkedCalc
yCalc

# OK?
# ---
Extract( odepth, y_aux, 0 )
#WriteXML( "ascii", y, "yREFERENCE_2D.xml" )
#WriteXML( "ascii", odepth, "y_auxREFERENCE_2D.xml" )
ReadXML( yREFERENCE, "yREFERENCE_2D.xml" )
VectorCompare( y, yREFERENCE, 1e-2 )
ReadXML( yREFERENCE, "y_auxREFERENCE_2D.xml" )
VectorCompare( odepth, yREFERENCE, 1e-3 )





#########################################################################
# 3D
#########################################################################

# Atmosphere and surface
# ---
AtmosphereSet3D
VectorLinSpace( lon_grid, -45, 45, 1 )
AtmFieldsCalcExpand1D
Extract( z_surface, z_field, 0 )

# Definition of sensor position and LOS
# ---
Append( sensor_pos, zeros, "trailing" )
Append( sensor_los, zeros, "trailing" )

# Perform RT calculations
# ---
basics_checkedCalc
cloudbox_checkedCalc
yCalc

# OK?
# ---
Extract( odepth, y_aux, 0 )
#WriteXML( "ascii", y, "yREFERENCE_3D.xml" )
#WriteXML( "ascii", odepth, "y_auxREFERENCE_3D.xml" )
ReadXML( yREFERENCE, "yREFERENCE_3D.xml" )
VectorCompare( y, yREFERENCE, 1e-2 )
ReadXML( yREFERENCE, "y_auxREFERENCE_3D.xml" )
VectorCompare( odepth, yREFERENCE, 1e-3 )

} # End of Main
 
