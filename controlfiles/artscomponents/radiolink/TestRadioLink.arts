#DEFINITIONS:  -*-sh-*-
#
# Demonstration and test of a single radio link calculation.
#
# Three different 1D cases are calculated.
#
# 2012-04-02, Patrick Eriksson

Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"


# Frequency grid 
#
VectorSet( f_grid, [10e9] )


# A pressure grid rougly matching 0 to 80 km, in steps of 250.
#
VectorNLogSpace( p_grid, 321, 1013e2, 1 )


# Definition of species
# 
SpeciesSet( abs_species,
            ["H2O-PWR98", 
             "N2-SelfContStandardType",
             "O2-PWR93"] )


# No line data needed here
# 
abs_lines_per_speciesSetEmpty


# Atmospheric profiles
# 
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, 
            "atmosphere_data/tropical" )
#
AtmFieldsCalc( t_field, z_field, vmr_field, p_grid, 
               lat_grid, lon_grid, t_field_raw, z_field_raw, 
               vmr_field_raw, atmosphere_dim, 3 )


# Surface altitude
MatrixSetConstant( z_surface, 1, 1, 0 )


# Check model atmosphere
# 
basics_checkedCalc
cloudbox_checkedCalc


# Propagation path agendas and variables
#
AgendaSet( ppath_step_agenda ){
  ppath_stepRefractionBasic
}
NumericSet( ppath_lmax, 10e3 )
NumericSet( ppath_lraytrace, 100 )
#
AgendaSet( ppath_agenda ){
  Ignore( rte_los )
  Ignore( cloudbox_on )
  Ignore( ppath_inside_cloudbox_do )
  VectorExtractFromMatrix( rte_pos2, transmitter_pos, mblock_index, "row" )
  ppathFromRtePos2
  #Print( ppath, 0 )
  #TangentPointPrint( ppath, 0 )
}


# Radiative transfer agendas and variables
#
VectorSet( rte_los, [] )   # Dummy value
FlagOff( dispersion_do )
#
AgendaSet( iy_space_agenda ){
  Ignore( rte_pos )
  Ignore( rte_los )
  MatrixUnitIntensity( iy, stokes_dim, f_grid )
}
AgendaSet( iy_clearsky_agenda ){
  Ignore( rte_los )
  iyRadioLink
}


# Postion of sensor/receiver and transmitter
#
# The transmisster cases are defined:
#   1: Ground-based
#   2: Air-borne (behind a tangent point)
#   3: Satellite
# 
VectorSet( rte_pos, [ 600e3 ] )
MatrixSet( transmitter_pos, [ 0, 2.3; 10e3, 25.7; 600e3, 48.3 ] )


# Expected transmissions
#
MatrixCreate( iyREFERENCE )
MatrixSet( iyREFERENCE, [ 0.98272, 0.84380, 0.71585 ] )
#MatrixCreate( iy_auxREFERENCE )
#MatrixSet( iy_auxREFERENCE, [ 7.4e-9, 204.7e-9, 636.8e-09 ] )


# Set up the ForLoop agenda to run and check the cases
#
IndexCreate( ilast )
nrowsGet( ilast, transmitter_pos )
IndexStepDown( ilast, ilast )
#
AgendaSet( forloop_agenda ){
  Copy( mblock_index, forloop_index )
  iyCalc
  #Print( iy, 0 )
  #Print( iy_aux, 0 )
  VectorCreate( iy_test)
  VectorExtractFromMatrix( iy_test, iy, 0, "column" )
  VectorCreate( iy_ref )
  VectorExtractFromMatrix( iy_ref, iyREFERENCE, mblock_index, "column" )
  VectorCompare( iy_test, iy_ref, 0.001 )
  #
  #VectorExtractFromMatrix( iy_test, iy_aux, 0, "column" )
  #VectorExtractFromMatrix( iy_ref, iy_auxREFERENCE, mblock_index, "column" )
  #VectorCompare( iy_test, iy_ref, 1e-9 )
}


# Better version if free space loss included
#AgendaSet( forloop_agenda ){
#  Copy( mblock_index, forloop_index )
#  iyCalc
#  Print( iy, 0 )
#  VectorCreate( iy_test)
#  VectorExtractFromMatrix( iy_test, iy, 0, "column" )
#  VectorCreate( iy_ref )
#  VectorExtractFromMatrix( iy_ref, iyREFERENCE, mblock_index, "column" )
#  NumericCreate( accuracy )
#  Extract( accuracy, iy_ref, 0 )
#  NumericScale( accuracy, accuracy, 0.001 )
#  VectorCompare( iy_test, iy_ref, accuracy )
#}


# Calculate
#
ForLoop( forloop_agenda, 0, ilast, 1  )


}
 
