#DEFINITIONS:  -*-sh-*-
#
# Demonstration and test of a single radio link calculations, using iyCalc.
#
# The link between a satellite and a surface point is simulated.
#
# 2012-08-21, Patrick Eriksson

Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"


# Number of Stokes components to be computed
#
IndexSet( stokes_dim, 1 )

# Frequency grid 
#
VectorSet( f_grid, [ 1e9, 5e9, 10e9 ] )


# A pressure grid rougly matching 0 to 80 km, in steps of 250.
#
VectorNLogSpace( p_grid, 321, 1013e2, 1 )


# Definition of species
# 
SpeciesSet( abs_species,
            ["H2O-PWR98", 
             "N2-SelfContStandardType",
             "O2-PWR93"] )


# No line data needed here
# 
abs_lines_per_speciesSetEmpty


# Dimensionality of the atmosphere
#
AtmosphereSet1D

# Atmospheric profiles
# 
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, 
            "testdata/tropical" )
#
AtmFieldsCalc( t_field, z_field, vmr_field, p_grid, 
               lat_grid, lon_grid, t_field_raw, z_field_raw, 
               vmr_field_raw, atmosphere_dim, 3 )


# Surface altitude
MatrixSetConstant( z_surface, 1, 1, 0 )


# No scattering
#
cloudboxOff

# No jacobian calculations
#
jacobianOff

# Check model atmosphere
# 
basics_checkedCalc
cloudbox_checkedCalc


# Propagation path agendas and variables
#
AgendaSet( ppath_step_agenda ){
  ppath_stepRefractionBasic
}
NumericSet( ppath_lmax, 10e3 )
NumericSet( ppath_lraytrace, 100 )
#
AgendaSet( ppath_agenda ){
  Ignore( rte_los )
  Ignore( cloudbox_on )
  Ignore( ppath_inside_cloudbox_do )
  ppathFromRtePos2
}


# Radiative transfer agendas and variables
#
VectorSet( rte_los, [] )   # Dummy value
#
AgendaSet( iy_transmitter_agenda ){
  Ignore( rte_pos )
  Ignore( rte_los )
  MatrixUnitIntensity( iy, stokes_dim, f_grid )
}
AgendaSet( iy_main_agenda ){
  Ignore( rte_los )
  iyRadioLink
}


# Postion of sensor/receiver and transmitter
#
VectorSet( rte_pos, [ 600e3 ] )
VectorSet( rte_pos2, [ 0, 8.3 ] )


# Auxilary variables
#
ArrayOfStringSet( iy_aux_vars, [ 
    "Pressure", "Temperature", "VMR, species 0", "Absorption, summed", 
    "Absorption, species 1", "Free space loss", "Atmospheric loss",
    "Defocusing loss", "Extra path delay", "Bending angle" ] )

# Run and save
#
iyCalc

WriteXML( "ascii", iy, "iy.xml" )
WriteXML( "ascii", iy_aux, "iy_aux.xml" )

}
 
