#DEFINITIONS:  -*-sh-*-
#This control file performs an ARTS-MC radiative transfer simulation
#through yCalc


Arts2 {

INCLUDE "general/general.arts"

#### LOAD DATA: these files were created with MCDataPrepare.arts ######

ReadXML( f_grid, "TestMonteCarloDataPrepare.f_grid.xml" )

ReadXML( p_grid, "p_grid.xml" )

AtmosphereSet3D

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

ReadXML( t_field, "TestMonteCarloDataPrepare.t_field.xml" )

ReadXML( z_field, "TestMonteCarloDataPrepare.z_field.xml" )

ReadXML( vmr_field, "TestMonteCarloDataPrepare.vmr_field.xml" )

ReadXML( z_surface, "TestMonteCarloDataPrepare.z_surface.xml" )

ReadXML( abs_lookup, "TestMonteCarloDataPrepare.abs_lookup.xml" )

SpeciesSet( abs_species, ["O2-PWR93","N2-SelfContStandardType","H2O-PWR98"] )

abs_lookupAdapt

FlagOn( cloudbox_on )
ReadXML( cloudbox_limits, "TestMonteCarloDataPrepare.cloudbox_limits.xml" )

ReadXML( pnd_field, "TestMonteCarloDataPrepare.pnd_field.xml" )

ReadXML( scat_data_raw, "TestMonteCarloDataPrepare.scat_data_raw.xml" )

basics_checkedCalc
cloudbox_checkedCalc


#### Define Agendas #################################################

AgendaSet( abs_mat_per_species_agenda ){
  Ignore(rte_mag)
  abs_mat_per_speciesInit
  abs_mat_per_speciesExtractFromLookup
}


#### Define viewing position and line of sight #########################

rte_losSet( rte_los, atmosphere_dim, 99.7841941981, 180 )

rte_posSet( rte_pos, atmosphere_dim, 95000.1, 7.61968838781, 0 )

Matrix1RowFromVector( sensor_pos, rte_pos )
Matrix1RowFromVector( sensor_los, rte_los )


#### Include an antenna pattern #########################################

IndexSet( stokes_dim, 4 )

AntennaSet1D
#
VectorSet( mblock_za_grid, [
 -0.2,-0.02,0.00,0.02,0.20
] )
#
MatrixSetConstant( antenna_los, 1, 1, 0 )

# An antenna pattern from Odin-SMR is used here
ReadXML( antenna_response, "antenna.SM_AC2ab.875ms.xml" )


IndexSet( sensor_norm, 1 )

sensor_responseInit
sensor_responseAntenna


#### Perform Monte Carlo RT Calculation #################################

StringSet( y_unit, "RJBT" )

NumericSet( mc_std_err,       -1 )
IndexSet(   mc_max_time,      -1 )
IndexSet(   mc_max_iter,      100 )
IndexSet(   mc_z_field_is_1D, 1 )

AgendaSet( iy_main_agenda ){
  Ignore( mblock_index )
  iyMC
  Touch( ppath )
}

yCalc



#### Save calculated Stokes vector and std. err.#########################

output_file_formatSetAscii

WriteXML( output_file_format, y )


#### Tests ########################

# Radiance test
#NumericCreate( y_error_0 )
#Extract( y_error_0, y_error, 0 )
#NumericScale( y_error_0, y_error_0, 4. )

#VectorCreate( y_0 )
#Select( y_0, y, [ 0 ] )

#VectorCreate( y_ref )
#VectorSetConstant( y_ref, 1, 199.5 )

#VectorCompare( y_0, y_ref, y_error_0,
#               "Total radiance should be close to 199.5 K" )

}

