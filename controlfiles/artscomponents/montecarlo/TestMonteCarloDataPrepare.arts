#DEFINITIONS:  -*-sh-*-
#This control file prepares a lot of atmospheric field data for the ARTS-MC examples
#simpleMCGeneral.arts, and simpleMCGeneralGaussian.arts, simpleMC.arts


Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"


output_file_formatSetBinary

VectorSet( f_grid, [ 230e9 ] )

WriteXML( output_file_format, f_grid )

IndexSet( f_index, 0 )

IndexSet( stokes_dim, 4 )

AtmosphereSet3D

ReadXML( p_grid, "p_grid.xml" )

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

SpeciesSet( abs_species,
            [ "O2-PWR93", "N2-SelfContStandardType", "H2O-PWR98" ] )

abs_lines_per_speciesSetEmpty

AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, "testdata/tropical" )

AtmFieldsCalcExpand1D

WriteXML( output_file_format, t_field )

WriteXML( output_file_format, z_field )

WriteXML( output_file_format, vmr_field )

nelemGet( nrows, lat_grid )

nelemGet( ncols, lon_grid )

MatrixSetConstant( z_surface, nrows, ncols, 500.0 )

WriteXML( output_file_format, z_surface )

abs_lookupSetup
abs_lookupCalc
WriteXML( output_file_format, abs_lookup )


cloudboxSetManually( cloudbox_on, cloudbox_limits, 
                     atmosphere_dim, 
                     p_grid, lat_grid, lon_grid,
                     21617.7922264, 17111.6808705,
                     -1.9, 1.9, -1.9, 1.9 )

Print( cloudbox_limits, 1 )

ParticleTypeInit

ParticleTypeAdd( scat_data_raw, pnd_field_raw, 
                 atmosphere_dim, 
                 f_grid, p_grid, lat_grid, lon_grid, 
                 cloudbox_limits,
                 "p30f229-231T214-225r100NP-1ar1_5ice.xml",
                 "" )

ReadXML( pnd_field_raw, "pnd_field_raw.xml" )

pnd_fieldCalc

WriteXML( output_file_format, pnd_field )

scat_data_monoCalc

#For some reason SingleScatteringData binary files can't be loaded 
output_file_formatSetAscii

WriteXML( output_file_format, scat_data_raw )

WriteXML( output_file_format, scat_data_mono )

WriteXML( output_file_format, cloudbox_limits )

}
