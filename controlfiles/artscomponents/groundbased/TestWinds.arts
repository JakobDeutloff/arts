#DEFINITIONS:  -*-sh-*-
# Simple simulations of ground-based measurements of ozone at 110.8 GHz,
# to test impact of winds.
#
# Author: Patrick Eriksson


Arts2 {

INCLUDE "general/general.arts"


# ---- f_grid ----------------------------------------------------------------

NumericCreate( v0 )
NumericSet( v0, 1.108360400e+11 )
VectorLinSpace( f_grid, -5e6, 5e6, 50e3 )
VectorAddScalar( f_grid, f_grid, v0 )


# ---- Species ---------------------------------------------------------------

SpeciesSet( abs_species, [ "O3", "H2O" ] )



# ---- Atmospheric scenario --------------------------------------------------

# A pressure grid rougly matching 0 to 88 km in about 1 km steps.
IndexCreate( np )
IndexSet( np, 90 )
VectorNLogSpace( p_grid, np, 1.013e5, 0.5 )

AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, 
                                  "atmosphere_data/tropical" )



# All settings here:
#
# ---- Select atmosphere_dim, LOS angles  and winds --------------------------

VectorSet( lat_grid, [-10, 10] )
VectorSet( lon_grid, [-10, 10] )

#AtmosphereSet1D
#AtmosphereSet2D
AtmosphereSet3D
MatrixSet( sensor_los, [80,45] )

#AtmFieldsCalc
AtmFieldsCalcExpand1D

nrowsGet( nrows, t_field )
ncolsGet( ncols, t_field )

Tensor3SetConstant( wind_u_field,np, nrows, ncols, 100 )
Tensor3SetConstant( wind_v_field,np, nrows, ncols, 100 )
Tensor3SetConstant( wind_w_field,np, nrows, ncols, 10 )



# ---- Absorption ------------------------------------------------------------

ReadXML( abs_lines, "ozone_line.xml" )
abs_lines_per_speciesCreateFromLines

abs_cont_descriptionInit

AgendaSet( abs_mat_per_species_agenda ){
  Ignore(rte_mag)
  abs_mat_per_speciesInit
  abs_mat_per_speciesAddLBL
}



# ---- The surface -----------------------------------------------------

MatrixSetConstant( z_surface, nrows, ncols, 0 )



# ---- Observation position ---------------------------------------------------

MatrixSetConstant( sensor_pos, 1, atmosphere_dim, 0 )



# ---- Final stuff -----------------------------------------------------------

basics_checkedCalc
cloudbox_checkedCalc
sensorOff
StringSet( y_unit, "RJBT" )



# ---- Calculate and save ----------------------------------------------------

yCalc

WriteXML( output_file_format, y, "TestWinds.y.xml" )
WriteXML( output_file_format, y_f, "TestWinds.y_f.xml" )

}
