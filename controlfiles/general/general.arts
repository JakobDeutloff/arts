#DEFINITIONS:  -*-sh-*-
#
# General ARTS defaults
#
# Authors: Stefan Buehler, Patrick Eriksson, Oliver Lemke

Arts2 {


#
# Initialize verbosity levels
#
verbosityInit


#
# Default output format
#
output_file_formatSetAscii


#
# Number of Stokes components to be computed
#
IndexSet( stokes_dim, 1 )


#
# Dimensionality of the atmosphere
#
AtmosphereSet1D


#
# The N2 VMR value is sometimes needed for the calculation of the
# absorption coefficient of other species. The value here is from
# Wallace&Hobbs, 2nd edition.
#
VectorSet( abs_n2, [ 0.7808 ] )


#
# The H2O VMR value is sometimes needed for the calculation of the
# absorption coefficient of other species. We cannot set this to a
# meaningful default value, since it is very variable. Set it to -1
# instead. 
#
VectorSet( abs_h2o, [ -1 ] )


#
# Lineshape function
#
# These should be the most accurate settings:
# Kuntz implementation of the Voigt function, 
# Van-Vleck and Huber normalization factor,
# cutoff at 750 GHz, consistent with Clough and Rosenkranz
#
abs_lineshapeDefine( abs_lineshape, "Voigt_Kuntz6", "VVH", 750e9 )


#
# Default is "on-the-fly" absorption
# Another option is to create an absorption table, which could save
# (considerable) time, especially for batch calculations.
# 

AgendaSet( abs_mat_per_species_agenda ){
  Ignore(rte_mag)
  abs_mat_per_speciesInit
  abs_mat_per_speciesAddLBL
  # To use the absorption lookup table, replace by:
  #abs_mat_per_speciesInit
  #abs_mat_per_speciesAddFromLookup
  # To get absorption from Zeeman tag,
  # uncomment this:
  #abs_mat_per_speciesAddZeemanLBL
  # and comment out Ignore(rte_mag):
}


#
# Set default interpolation orders for absorption lookup table. Do not
# mess with these values, unless you know what you are doing! 
#
IndexSet( abs_p_interp_order,   5 )
IndexSet( abs_t_interp_order,   7 )
IndexSet( abs_nls_interp_order, 5 )


#
# Define reference ellipsoid (a spherical ellipsoid must be used for 1D)
# 
refellipsoidEarth( refellipsoid, "Sphere" )


#
# Gravity and weight of dry air
# (needed for hydrostatic equilibrium calculations)
# 
AgendaSet( g0_agenda ){
   Ignore( lon )
   g0Earth
} 
NumericSet( molarmass_dry_air, 28.966 )


# 
# Winds (all set to be empty, shorthand for no winds)
#
Tensor3SetConstant( wind_u_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( wind_v_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( wind_w_field, 0, 0, 0, 0.0 )


# 
# Magnetic field (all components set to be empty, shorthand for no magnetic field)
#
Tensor3SetConstant( mag_u_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( mag_v_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( mag_w_field, 0, 0, 0, 0.0 )


# 
# Field of free electrons (empty is shorthand for no free electrons)
#
Tensor3SetConstant( edensity_field, 0, 0, 0, 0.0 )


#
# Surface, upwelling radiation
# 
# The default model is a blackbody surface. This is a good choice only
# if your sensor operates at a frequency where the troposphere is
# opaque, but it is the fastest option.
#
# Warning: If you want to do calculations with something other than a
# blackbody surface, you have to re-define the surface_rtprop_agenda. It
# is not enough to just set e.g. surface_scalar_reflectivity! 
# 
AgendaSet( iy_surface_agenda ){
  iySurfaceRtpropAgenda
} 
AgendaSet( surface_rtprop_agenda ){
   Ignore( rte_los )
   InterpAtmFieldToRtePos( surface_skin_t, atmosphere_dim, p_grid, lat_grid,
                           lon_grid, z_field, rte_pos, t_field )
   surfaceBlackbody
} 


#
# Default is to include emission in source term
# 
AgendaSet( blackbody_radiation_agenda ){
  blackbody_radiationPlanck
}



#
# Refractive index agenda
# (not used if ppath_stepGeometric used in ppath_step_agenda)
#
AgendaSet( refr_index_agenda ){
  Ignore( f_grid )
  Ignore( rte_edensity )
  NumericSet( refr_index, 1.0 )
  NumericSet( refr_index_group, 1.0 )
  refr_indexThayer
}


#
# Agendas and variables for calculation of propagation paths:
# (This will result in pure geometrical calculations. That is, refraction is 
# neglected. The value for ppath_lmax of 10e3 is OK for limb sounding, and 
# also for down-looking if not very accutate results are demanded.
# You should not change the value of ppath_inside_cloudbox_do, if you don't 
# know exactly what you are doing!)
#
AgendaSet( ppath_step_agenda ){
  Ignore( t_field )
  Ignore( vmr_field )
  Ignore( edensity_field )
  Ignore( f_grid )
  ppath_stepGeometric
}
NumericSet( ppath_lmax, 10e3 )
#
AgendaSet( ppath_agenda ){
  Ignore( rte_pos2 )
  ppathStepByStep
}
FlagOff( ppath_inside_cloudbox_do )


#
# Agendas for clearsky radiative transfer calculation
# 
AgendaSet( iy_space_agenda ){
  Ignore( rte_pos )
  Ignore( rte_los )
  MatrixCBR( iy, stokes_dim, f_grid )
}
#
AgendaSet( iy_main_agenda ){
  iyEmissionStandard
}


#
# No auxiliary variables as default
#
ArrayOfStringSet( iy_aux_vars, [] )


#
# No unit conversion 
#
StringSet( y_unit, "1" )


#
# No scattering
#
cloudboxOff
ParticleSpeciesSet( part_species, [] )


#
# No jacobian calculations by default
#
jacobianOff


#
# Batch calculations start at index 0 by default
#
IndexSet( ybatch_start, 0 )


#
# Default is that no transmitter is involved
#
MatrixSet( transmitter_pos, [] )


#
# FOS 
#
IndexSet( fos_i, 0 )

#
# Isotopologue ratios
#
isotopologue_ratiosInitFromBuiltin

}

