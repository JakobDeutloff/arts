#DEFINITIONS:  -*-sh-*-
#
# General ARTS defaults
#
# Authors: Stefan Buehler, Patrick Eriksson, Oliver Lemke

Arts2 {



############
# PART 1 - following settings serve (mainly) as initialisations
############

#
# Initialize verbosity levels
#
verbosityInit

#
# No particle/scattering species (yet)
#
ParticleSpeciesSet( part_species, [] )

#
# Default is that no transmitter is involved
#
MatrixSet( transmitter_pos, [] )

#
# FOS 
#
IndexSet( fos_i, 0 )

#
# No auxiliary variables as default
#
ArrayOfStringSet( iy_aux_vars, [] )

#
# The H2O VMR value is sometimes needed for the calculation of the
# absorption coefficient of other species. We cannot set this to a
# meaningful default value, since it is very variable. Set it to -1
# instead. 
#
VectorSet( abs_h2o, [ -1 ] )

# jm: in this basic version here as initialisation. also, no refraction (n=1)
#  seems a sensible default setting.
#
# Refractive index agenda
#
AgendaSet( refr_index_agenda ){
  Ignore( f_grid )
  Ignore( rte_edensity )
  Ignore( rte_pressure )
  Ignore( rte_temperature )
  Ignore( rte_vmr_list )
  NumericSet( refr_index, 1.0 )
  NumericSet( refr_index_group, 1.0 )
}


### basics_CheckedCalc requires them to be initialised. 0 seems an ok default.
# 
# Winds, Magnetic field, and electron density
# (all components set to be empty, shorthand for no winds/magnetic field)
#
Tensor3SetConstant( wind_u_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( wind_v_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( wind_w_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( mag_u_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( mag_v_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( mag_w_field, 0, 0, 0, 0.0 )
Tensor3SetConstant( edensity_field, 0, 0, 0, 0.0 )



############
# PART 2 - following settings are highly recommended to be NOT changed
############

#
# Set default interpolation orders for absorption lookup table. Do not
# mess with these values, unless you know what you are doing! 
#
IndexSet( abs_p_interp_order,   5 )
IndexSet( abs_t_interp_order,   7 )
IndexSet( abs_nls_interp_order, 5 )

#
# Variable for calculation of propagation paths:
#
# You should not change the value of ppath_inside_cloudbox_do, if you don't 
# know exactly what you are doing!)
FlagOff( ppath_inside_cloudbox_do )



############
# PART 3 - following settings define some meaningful defaults
############

#
# Default output format
#
output_file_formatSetAscii


### jm: general enough? or throw away & leave setting completely to user?
#
# Lineshape function
#
# These should be the most accurate settings:
# Kuntz implementation of the Voigt function, 
# Van-Vleck and Huber normalization factor,
# cutoff at 750 GHz, consistent with Clough and Rosenkranz
#
abs_lineshapeDefine( abs_lineshape, "Voigt_Kuntz6", "VVH", 750e9 )


### jm: this one general enough? suggest to leave here as unexperienced user might
#  not have a good feeling for what is an appropriate value.
#
# Variable for calculation of propagation paths:
#
# The value for ppath_lmax of 10e3 is OK for limb sounding, and 
# also for down-looking if not very accutate results are demanded.
#
NumericSet( ppath_lmax, 10e3 )


#
# No unit conversion 
#
StringSet( y_unit, "1" )

#
# Batch calculations start at index 0 by default
#
IndexSet( ybatch_start, 0 )


INCLUDE "agendas.arts"
INCLUDE "general_topical.arts"
}

