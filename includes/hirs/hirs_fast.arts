#DEFINITIONS:  -*-sh-*-

# ARTS setup file for fast HIRS simulations.
#
# Use this if you want to do fast HIRS simulations.  It should be a
# good approximation of the exact simulations.
#
# This expects a number of workspace variables to exist and to be set:
# 
# satellite   (String) 	     The name of the satellite. Is
#                      	     used internally to construct
#                      	     file names of the instrument
#                      	     description files
#                      	     f_backend_file and
#                      	     backend_channel_response_file. 
# channels    (ArrayOfIndex) Which channels you want.
# views       (ArrayOfIndex) Which views you want.
# hitran_file (String)       Name of HITRAN catalogue file.
# f_grid_spacing (Numeric)   Frequency grid spacing.

# Actually the fast calculation uses a pre-defined f_grid, so that
# f_grid_spacing would not be necessary. However, we first generate
# a template sensor_response with the normal sensor_responseBackend
# function, and for that we need it.

Arts2 {


# 1. General
#-----------

output_file_formatSetZippedAscii

# AMSU uses Planck brightness temperatures
# ---
StringSet( y_unit, "PlanckBT" )
StringSet( jacobian_unit, "RJBT" )


# 2. Spectroscopy
#----------------

SpeciesSet( abs_species, [ "H2O, H2O-SelfContCKDMT100, H2O-ForeignContCKDMT100", 
	                   "O3", 
                           "CO2, CO2-CKDMT100", 
                           "N2O", 
                           "CO", 
                           "CH4", 
                           "O2, O2-CIAfunCKDMT100", 
                           "N2, N2-CIAfunCKDMT100, N2-CIArotCKDMT100" ] )

# abs_lineshape_per_tgDefine( abs_lineshape, 
#                             abs_species,
#                             ["Voigt_Kuntz6", "Voigt_Kuntz6", "Voigt_Kuntz6", "Voigt_Kuntz6", 
# 			     "Voigt_Kuntz6", "Voigt_Kuntz6", "Voigt_Kuntz6", "Voigt_Kuntz6"],
#                             ["VVH", "VVH", "VVH", "VVH", "VVH", "VVH", "VVH", "VVH"],
#                             [750e9, 750e9, 750e9, 750e9, 750e9, 750e9, 750e9, 750e9] )
abs_lineshapeDefine( abs_lineshape, "Voigt_Kuntz6", "VVH", 750e9 )

# Read HITRAN catalog:
abs_linesReadFromHitran2004( abs_lines,
                             hitran_file,
                             2.0022234e+12,
                             7.9855733e+14 )

abs_lines_per_speciesCreateFromLines


# WARNING: If you redifine abs_species, and want to do a line-by-line
# calculation, you also have to call
# abs_lines_per_speciesCreateFromLines again.


# 3. Sensor:
#-----------

# Definition of sensor position and LOS
# ---
ReadXML( sensor_los, "hirs/hirs.sensor_los.xml" )

# Select those views that are requested by the user
Select( sensor_los, sensor_los, views )

nrowsGet( nrows, sensor_los )
ncolsGet( ncols, sensor_los )
MatrixSetConstant( sensor_pos, nrows, ncols, 850e3 )
sensor_posAddRgeoid


# Start sensor response setup
# ---

# Normalise the sensor response
# ---
IndexSet( sensor_norm, 1 )

# Antenna
# ---
AntennaOff


# See setup_input.m for details around other sensor variables 


# Construct names of sensor description files:
 
# Nominal channel frequencies:
StringCreate( f_backend_file)
StringSet(    f_backend_file, "hirs/")
Append(       f_backend_file, satellite)
# FIXME: Remove this when using constant in Append works:
StringCreate( dummy )
StringSet(    dummy,          "_HIRS.f_backend.xml")
#
Append(       f_backend_file, dummy)

# Channel response functions:
StringCreate( backend_channel_response_file)
StringSet(    backend_channel_response_file, "hirs/")
Append(	      backend_channel_response_file, satellite)
# FIXME: Remove this when using constant in Append works:
StringSet(    dummy,          "_HIRS.backend_channel_response.xml")
#
Append(	      backend_channel_response_file, dummy)



# Spectrometer:
#
ReadXML( f_backend, f_backend_file )
ReadXML( backend_channel_response,
         backend_channel_response_file )



# Read also the specific input files for fast calculation, the
# pseudo-K frequency grid and weights.
 
# Frequency grid
StringCreate( f_grid_file)
StringSet(    f_grid_file, "hirs/")
Append(       f_grid_file, satellite)
# FIXME: Remove this when using constant in Append works:
StringCreate( dummy )
StringSet(    dummy,          "_HIRS.f_grid_fast.xml")
#
Append(       f_grid_file, dummy)

# Weights associated with each frequency
StringCreate( weights_file)
StringSet(    weights_file, "hirs/")
Append(	      weights_file, satellite)
# FIXME: Remove this when using constant in Append works:
StringSet(    dummy,          "_HIRS.W_fast.xml")
#
Append(	      weights_file, dummy)


# Frequency grid
#
# We need this here, so that sensor_responseBackend works
# correctly. Afterwards, we replace it by the optimized frequency
# grid. 
f_gridFromSensorHIRS( f_grid,
                      f_backend, backend_channel_response,
		      f_grid_spacing  )


# Construct sensor response from all these inputs:
#
sensor_responseInit
sensor_responseBackend

# Read in optimized frequency grid and pseudo-k weights:

ReadXML( f_grid, f_grid_file )

SparseCreate(pseudo_k_weights)
ReadXML( pseudo_k_weights, weights_file )



# Now we have to replace the actual response matrix by the pseudo_k_weights:
#WriteXML(output_file_format, sensor_response)
Copy(sensor_response, pseudo_k_weights)


# FIXME: Still to do: Kick out those frequencies that are not used by
# the selected channels. This has to be done for f_grid and pseudo_k_weights.


# End of sensor response setup


# Compact line list, to kick out lines that are outside there 
# cutoff range for all frequencies.
abs_lines_per_speciesCompact

# If there was a method to convert abs_lines_per_species back to
# abs_lines, then we could do that, and save that file. The only
# problem is, though, that the lines to include are not exactly the same
# for all the different versions of the HIRS sensor. Thus, one would
# also have to perform a merge of all the different resulting line
# lists. :-(

}
