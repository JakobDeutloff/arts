# ARTS setup file for simulations of Odin-SMR measurements around 501.8 GHz.
# This band is part of the stratospheric mode, denoted as SM_AC2ab.
#
# The simulations are intended to match the operational settings, but there
# are some differences. See notes below.
#
# Input files are found in the folder tests/OdinSMR.
# See setup_input.m for source of input files.
#
# Author: Patrick Eriksson


Arts2 {

INCLUDE "general.arts"
INCLUDE "odinsmr.arts"


# A number of species with very weak emission are here neglected
SpeciesSet( abs_species, [
  "H2O,H2O-ForeignContStandardType,H2O-SelfContStandardType",
  "N2-SelfContMPM93",
  "O2,O2-PWR98",
  "O3",
  "HNO3",
  "N2O" 
] )

# Line file
abs_linesReadFromArts{ abs_lines, "linefile.SM_AC1e.al", 0, 2e12 )



# Frequency grid
ReadXML( f_grid, "f_mono.SM_AC1e.xml" )



#
# Sensor characteristics
# 

IndexSet( sensor_norm, 1 )

# Antenna:
#
AntennaSet1D
#
VectorSetExplicitly( mblock_za_grid, [
  -0.2,-0.15,-0.1,-0.05,-0.04,-0.03,-0.02,-0.01,
  0,0.01,0.02,0.03,0.04,0.05,0.10,0.15,0.20
] )
#
MatrixSet( antenna_los, 1, 1, 0 )

# The folder contains antenna pattern for different integration times.
# The pattern for smallest integration time is selected here.
ReadXML( antenna_diagram, "antenna.SM_AC1e.875ms.xml" )


# Polarisation:
#
MatrixSet( sensor_pol, 1, 1, 1 )

# Mixer:
#
VectorSetExplicitly( lo, [548.502e9] )
MatrixCreate( sideband_response )
ReadXML( sideband_response, "sideband.SM_AC1e.xml" )
StringSet( sideband_mode, "lower" )

# Spectrometer:
#
ReadXML( f_backend, "f_backend.SM_AC1e.xml" )
ArrayOfMatrixCreate( backend_channel_response )
ReadXML( backend_channel_response, "backend.xml" )


sensor_responseInit
sensor_responseAntenna1D
sensor_responseMixer( 
  sensor_response, sensor_response_f, f_mixer,
  sensor_response_pol, sensor_response_za, lo, sensor_norm,
  sideband_response 
)
sensor_responseBackend( 
  sensor_response, sensor_response_f,
  f_backend, sensor_response_pol, sensor_response_za, sensor_norm,
  backend_channel_response
)

} # End Arts
