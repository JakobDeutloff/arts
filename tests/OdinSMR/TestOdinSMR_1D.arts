# This is a test doing Odin-SMR simulations. Same test as in
# testOdinSMR, but the calculations are here performed inside a single
# measurement block. This approach can only be used for 1D.
#
# Author: Patrick Eriksson


Arts2 {

# Select frequency band here:
#
INCLUDE "odinsmr_501_1D.arts"


# Possibility to change considered species
# For example
#SpeciesSet( abs_species, [
#  "H2O,H2O-ForeignContStandardType,H2O-SelfContStandardType",
#  "O3" 
#] )



# ---- Atmospheric scenario --------------------------------------------------

# A pressure grid rougly matching 0 to 80 km in 250 m steps.
# The pressure grid is for the SMR processing not uniform. It is there
# created to be most dense over the actual range of tangent altitudes.
#
VectorNLogSpace( p_grid, 321, 1000e2, 1 )

# This example assumes 1D
AtmosphereSet1D

# Atmospheric profiles here taken from Fascod
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, "tropical" )
#
AtmFieldsCalc

# Get ground altitude (z_surface) from z_field
MatrixExtractFromTensor3( z_surface, z_field, 0 )



# ---- Create absorption table -----------------------------------------------

abs_lines_per_speciesCreateFromLines

AbsInputFromAtmFields
SpeciesSet( abs_nls, [] )
VectorSetExplicitly( abs_nls_pert, [] )
VectorSetExplicitly( abs_t_pert, [] )

abs_lookupCreate

AgendaSet( abs_scalar_gas_agenda ){
  abs_scalar_gasExtractFromLookup
}



# ---- Sensor position and LOS -----------------------------------------------

# Tangent altitudes are here determined when creating sensor response matrix.
# The tangent altitudes are stored in *vector_2*.


# ---- Calculate, switch to RF and save

RteCalc

ConvertIFToRF

WriteXML( output_file_format, sensor_response_f, ""                        )
WriteXML( output_file_format, z_tan,             "TestOdinSMR_1D.ztan.xml" )
WriteXML( output_file_format, y,                 ""                        )

}
