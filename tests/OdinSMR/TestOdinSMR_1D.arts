# This is a test doing Odin-SMR simulations. Same test as in
# testOdinSMR, but the calculations are here performed inside a single
# measurement block. This approach can only be used for 1D.
#
# Author: Patrick Eriksson


Arts2 {

# Select frequency band here:
#
INCLUDE "odin/odinsmr_501_1D.arts"


# Possibility to change considered species
# For example
#SpeciesSet( abs_species, [
#  "H2O,H2O-ForeignContStandardType,H2O-SelfContStandardType",
#  "O3" 
#] )



# ---- Atmospheric scenario --------------------------------------------------

# A pressure grid rougly matching 0 to 80 km in 250 m steps.
# The pressure grid is for the SMR processing not uniform. It is there
# created to be most dense over the actual range of tangent altitudes.
#
VectorNLogSpace( p_grid, 321, 1000e2, 1 )

# This example assumes 1D
AtmosphereSet1D

# Atmospheric profiles here taken from Fascod
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, "tropical" )
#
AtmFieldsCalc

# Get ground altitude (z_surface) from z_field
Extract( z_surface, z_field, 0 )



# ---- Create absorption table -----------------------------------------------

abs_lines_per_speciesCreateFromLines

AbsInputFromAtmFields
SpeciesSet( abs_nls, [] )
VectorSet( abs_nls_pert, [] )
VectorSet( abs_t_pert, [] )

abs_lookupCreate

AgendaSet( abs_scalar_gas_agenda ){
  abs_scalar_gasExtractFromLookup
}



# ---- Sensor position and LOS -----------------------------------------------

# Tangent altitudes are here determined when creating sensor response matrix.
# The tangent altitudes are stored in *vector_2*.



# ---- Calculate and save

RteCalc


WriteXML( output_file_format, y,     "TestOdinSMR.y.xml.generated" )

WriteXML( output_file_format, z_tan, "TestOdinSMR.ztan.xml.generated" )

WriteXML( output_file_format, sensor_response_f_grid, 
                            "TestOdinSMR.sensor_response_f_grid.xml.generated" )

WriteXML( output_file_format, y_f,   "TestOdinSMR.y_f.xml.generated"   )
WriteXML( output_file_format, y_pol, "TestOdinSMR.y_pol.xml.generated" )
WriteXML( output_file_format, y_pos, "TestOdinSMR.y_pos.xml.generated" )
WriteXML( output_file_format, y_los, "TestOdinSMR.y_los.xml.generated" )
}
