#DEFINITIONS:  -*-sh-*-
#This control file perfoems an ARTS-MC radiative transfer simulation
#For a single line of sight and a Pencil Beam antenna response


Arts2 {

INCLUDE "general.arts"

#### LOAD DATA: these files were created with MCDataPrepare.arts ######

ReadXML( f_grid, "TestMonteCarloDataPrepare.f_grid.xml" )

IndexSet( f_index, 0 )

ReadXML( p_grid, "p_grid.xml" )

AtmosphereSet3D

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

ReadXML( t_field, "TestMonteCarloDataPrepare.t_field.xml" )

ReadXML( z_field, "TestMonteCarloDataPrepare.z_field.xml" )

ReadXML( vmr_field, "TestMonteCarloDataPrepare.vmr_field.xml" )

r_geoidSpherical( r_geoid, atmosphere_dim, lat_grid, lon_grid, -1 )

ReadXML( z_surface, "TestMonteCarloDataPrepare.z_surface.xml" )

ReadXML( abs_lookup, "TestMonteCarloDataPrepare.abs_lookup.xml" )

SpeciesSet( abs_species,
            [ "O2-PWR93", "N2-SelfContStandardType", "H2O-PWR98" ] )

abs_lookupAdapt

FlagOn( cloudbox_on )
ReadXML( cloudbox_limits, "TestMonteCarloDataPrepare.cloudbox_limits.xml" )

ReadXML( pnd_field, "TestMonteCarloDataPrepare.pnd_field.xml" )

ReadXML( scat_data_mono, "TestMonteCarloDataPrepare.scat_data_mono.xml" )


#### Define Agendas #################################################

AgendaSet( abs_scalar_gas_agenda ){
  abs_scalar_gasExtractFromLookup
}


#### Define viewing position and line of sight #########################

rte_losSet( rte_los, atmosphere_dim, 99.7841941981, 180 )

rte_posSet( rte_pos, atmosphere_dim, 95000.1, 7.61968838781, 0 )

rte_posAddRgeoid

Matrix1RowFromVector( sensor_pos, rte_pos )

Print( sensor_pos, 1 )

Matrix1RowFromVector( sensor_los, rte_los )

Print( sensor_los, 1 )


#### Set some Monte Carlo parameters ###################################

IndexSet( stokes_dim, 4 )

StringSet( y_unit, "RJBT" )

MCSetSeedFromTime

Print( mc_seed, 1 )

mc_antennaSetPencilBeam

#### Check atmosphere ##################################################

basics_checkedCalc
cloudbox_checkedCalc


#### Perform Monte Carlo RT Calculation #################################

NumericSet( mc_std_err, -1 )
IndexSet( mc_max_time, 20 )
IndexSet( mc_max_iter, -1 )
IndexSet( mc_z_field_is_1D, 1 )

MCGeneral

#### Save calculated Stokes vector and std. err.#########################

output_file_formatSetAscii
WriteXML( output_file_format, y )

WriteXML( output_file_format, mc_error )

#### Print number of photons and radiance units ########################

Print( mc_iteration_count, 1 )

Print( y_unit, 1 )

}

