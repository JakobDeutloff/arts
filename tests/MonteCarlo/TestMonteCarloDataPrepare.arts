#DEFINITIONS:  -*-sh-*-
#This control file prepares a lot of atmospheric field data for the ARTS-MC examples
#simpleMCGeneral.arts, and simpleMCGeneralGaussian.arts, simpleMC.arts


Arts2 {

INCLUDE "general.arts"
INCLUDE "continua.arts"


output_file_formatSetBinary

VectorSetExplicitly( f_grid, [ 230e9 ] )

WriteXML( output_file_format, f_grid,  "f_grid.xml.generated" )

IndexSet( f_index, 0 )

IndexSet( stokes_dim, 4 )

AtmosphereSet3D

ReadXML( p_grid, "p_grid.xml" )

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

SpeciesSet( abs_species,
            [ "O2-PWR93", "N2-SelfContStandardType", "H2O-PWR98" ] )

abs_lines_per_speciesSetEmpty

AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, "tropical" )

AtmFieldsCalcExpand1D

WriteXML( output_file_format, t_field, "t_field.xml.generated" )

WriteXML( output_file_format, z_field, "z_field.xml.generated" )

WriteXML( output_file_format, vmr_field, "vmr_field.xml.generated" )

r_geoidSpherical( r_geoid, atmosphere_dim, lat_grid, lon_grid, -1 )

nrowsGet( nrows, r_geoid )

ncolsGet( ncols, r_geoid )

MatrixSet( z_surface, nrows, ncols, 500.0 )

WriteXML( output_file_format, z_surface, "z_surface.xml.generated" )

#AgendaSet( surface_prop_agenda ){
#  InterpAtmFieldToRteGps( surface_skin_t,
#                          atmosphere_dim, 
#                          p_grid, lat_grid, lon_grid, 
#                          rte_gp_p, rte_gp_lat, rte_gp_lon, 
#                          t_field )
#  surfaceBlackbody
#  Ignore( rte_los )
#}

abs_lookupSetup
abs_lookupCreate
WriteXML( output_file_format, abs_lookup, "abs_lookup.xml.generated" )


cloudboxSetManually( cloudbox_on, cloudbox_limits, 
                     atmosphere_dim, 
                     p_grid, lat_grid, lon_grid,
                     21617.7922264, 17111.6808705,
                     -1.8, 1.8, -1.8, 1.8 )

Print( cloudbox_limits, 1 )

ParticleTypeInit

ParticleTypeAdd( scat_data_raw, pnd_field_raw, 
                 atmosphere_dim, 
                 f_grid, p_grid, lat_grid, lon_grid, 
                 cloudbox_limits,
                 "p30f229-231T214-225r100NP-1ar1_5ice.xml",
                 "" )

ReadXML( pnd_field_raw, "pnd_field_raw.xml" )

pnd_fieldCalc

WriteXML( output_file_format, pnd_field, "pnd_field.xml.generated" )

scat_data_monoCalc

#For some reason SingleScatteringData binary files can't be loaded 
output_file_formatSetAscii

WriteXML( output_file_format, scat_data_raw, "scat_data_raw.xml.generated" )

WriteXML( output_file_format, scat_data_mono, "scat_data_mono.xml.generated" )

WriteXML( output_file_format, cloudbox_limits, "cloudbox_limits.xml.generated" )

}
