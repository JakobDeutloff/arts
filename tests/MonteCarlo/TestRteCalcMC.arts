#DEFINITIONS:  -*-sh-*-
#This control file performs an ARTS-MC radiative transfer simulation
#through the WSM RteCalcMC


Arts2 {

INCLUDE "general.arts"

#### LOAD DATA: these files were created with MCDataPrepare.arts ######

ReadXML( f_grid, "f_grid.xml.generated" )

ReadXML( p_grid, "p_grid.xml" )

AtmosphereSet3D

ReadXML( lat_grid, "lat_grid.xml" )

ReadXML( lon_grid, "lon_grid.xml" )

ReadXML( t_field, "t_field.xml.generated" )

ReadXML( z_field, "z_field.xml.generated" )

ReadXML( vmr_field, "vmr_field.xml.generated" )

r_geoidSpherical( r_geoid, atmosphere_dim, lat_grid, lon_grid, -1 )

ReadXML( z_surface, "z_surface.xml.generated" )

ReadXML( abs_lookup, "abs_lookup.xml.generated" )

SpeciesSet( abs_species, ["O2-PWR93","N2-SelfContStandardType","H2O-PWR98"] )

abs_lookupAdapt

FlagOn( cloudbox_on )
ReadXML( cloudbox_limits, "cloudbox_limits.xml.generated" )

ReadXML( pnd_field, "pnd_field.xml.generated" )

ReadXML( scat_data_raw, "scat_data_raw.xml.generated" )


#### Define Agendas #################################################

AgendaSet( abs_scalar_gas_agenda ){
  abs_scalar_gasExtractFromLookup
}


#### Define viewing position and line of sight #########################

rte_losSet( rte_los, atmosphere_dim, 99.7841941981, 180 )

rte_posSet( rte_pos, atmosphere_dim, 95000.1, 7.61968838781, 0 )

rte_posAddRgeoid

Matrix1RowFromVector( sensor_pos, rte_pos )
Matrix1RowFromVector( sensor_los, rte_los )


#### Include an antenna pattern #########################################

IndexSet( stokes_dim, 4 )

AntennaSet1D
#
VectorSet( mblock_za_grid, [
 -0.2,-0.02,0.00,0.02,0.20
] )
#
MatrixSetConstant( antenna_los, 1, 1, 0 )

# An antenna pattern from Odin-SMR is used here
ReadXML( antenna_response, "antenna.SM_AC2ab.875ms.xml" )


IndexSet( sensor_norm, 1 )

sensor_responseInit
sensor_responseAntenna


#### Perform Monte Carlo RT Calculation #################################

StringSet( y_unit, "RJBT" )

NumericSet( mc_std_err,       -1 )
IndexSet(   mc_max_time,      -1 )
IndexSet(   mc_max_iter,      100 )
IndexSet(   mc_z_field_is_1D, 1 )

AgendaSet( iy_clearsky_agenda ){
  iyMC
}

atm_checkedCalc

yCalc



#### Save calculated Stokes vector and std. err.#########################

output_file_formatSetAscii

WriteXML( output_file_format, y, "RteCalcMC.y.xml.generated" )

WriteXML( output_file_format, y_error, "RteCalcMC.mc_error.xml.generated" )

}

