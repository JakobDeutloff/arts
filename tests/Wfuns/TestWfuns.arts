#DEFINITIONS:  -*-sh-*-
# This is a test of weighting function calculations.
#
# The test is baded on the Odin-SMR 501 GHz case found in another folder.
#
# Author: Patrick Eriksson


Arts2 {


##############################################################################
#
# Initial part
#
##############################################################################


# Select frequency band here:
#
INCLUDE "odin/odinsmr_501.arts"



# ---- Atmospheric scenario --------------------------------------------------

# A pressure grid rougly matching 0 to 80 km in 250 m steps.
#
VectorNLogSpace( p_grid, 321, 1000e2, 1 )

# 1D atmosphere
#
AtmosphereSet1D

# Atmospheric profiles here taken from Fascod
AtmRawRead( t_field_raw, z_field_raw, vmr_field_raw, abs_species, "../atmosphere_data/tropical" )
#
AtmFieldsCalc

# Get ground altitude (z_surface) from z_field
Extract( z_surface, z_field, 0 )

basics_checkedCalc
cloudbox_checkedCalc



# ---- Create absorption table -----------------------------------------------

abs_lines_per_speciesCreateFromLines

AbsInputFromAtmFields
SpeciesSet( abs_nls, [] )
VectorSet( abs_nls_pert, [] )
VectorSet( abs_t_pert, [] )

abs_lookupCreate

AgendaSet( abs_scalar_gas_agenda ){
  abs_scalar_gasExtractFromLookup
}



# ---- Sensor position and LOS -----------------------------------------------

# Number of tangent altitudes
IndexCreate( n_tan )
IndexSet( n_tan, 3 )

# Sensor position, with platform altitude set to 600 km
MatrixSetConstant( sensor_pos, n_tan, 1, 600e3 )
sensor_posAddRgeoid

# LOS, specified by the corresponding geometrical tangent altitudes
# Tangent altitudes will be equally spaced between 50 and 20 km
VectorCreate( z_tan )
VectorNLinSpace( z_tan, n_tan, 50e3, 20e3 )
VectorCreate( za )
VectorZtanToZa1D( za, sensor_pos, r_geoid, atmosphere_dim, z_tan )
Matrix1ColFromVector( sensor_los, za )





##############################################################################
#
# Compare where two different calculation versions exist
#
##############################################################################

StringCreate( anal_or_pert )
VectorCreate( retrieval_grid )
MatrixCreate( Ja )
MatrixCreate( Jp )

# Analytical:
StringSet( anal_or_pert, "analytical" )
INCLUDE "common_wfuns_defs.arts"
Copy( Ja, jacobian )

# Perurbations:
StringSet( anal_or_pert, "perturbation" )
INCLUDE "common_wfuns_defs.arts"
Copy( Jp, jacobian )

# Compare
MatrixCompare( Ja, Jp, 0.01 )





##############################################################################
#
# Go though other weighting functions
#
##############################################################################

jacobianInit

nrowsGet( nrows, sensor_pos )
VectorNLinSpace( sensor_time, nrows, 0, 1 )

jacobianAddPointingZa( jacobian_quantities, jacobian_agenda, 
                       sensor_pos, sensor_time, 0, "iybrecalc", 0.01 )

# Another test of jacobianAddFreqShiftAndStretch is found in Groundbased
jacobianAddFreqShiftAndStretch( jacobian_quantities, jacobian_agenda, 
                                "iybinterp", 50e3, 1 )

jacobianAddPolyfit( jacobian_quantities, jacobian_agenda, 
    sensor_response_pol_grid, sensor_response_f_grid, 
    sensor_response_za_grid, sensor_pos, 1, 0, 0, 0 )

jacobianClose


}
